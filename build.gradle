apply plugin: "java"
apply plugin: "idea"
apply plugin: "eclipse"

task wrapper(type: Wrapper) {
    gradleVersion = "1.2"
    jarFile = "gradle/gradle-wrapper.jar"
}

repositories {
    mavenCentral()
}

ext.seleniumVersion = "2.25.0"
ext.junitVersion = "4.8+"
ext.jettyVersion = "6.1.21"

dependencies {
    ["selenium-java", "selenium-remote-driver", "selenium-server"].each {
        compile "org.seleniumhq.selenium:$it:$seleniumVersion"
        testCompile "org.seleniumhq.selenium:$it:$seleniumVersion"
    }
    testCompile "junit:junit-dep:$junitVersion"
	testCompile "org.mortbay.jetty:jetty:$jettyVersion"
}

tasks.withType(JavaExec) {
    classpath = configurations.compile + sourceSets.test.output
    args project.hasProperty("args") ? project.args.split("\\s") : []
}

test {
    //makes the standard streams (err and out) visible at console when running tests
    testLogging.showStandardStreams = true

    //listening to test execution events
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}
